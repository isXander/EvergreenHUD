pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()

        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            name = 'JitPack'
            url = 'https://jitpack.io'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven/'
        }

        flatDir dirs: 'libs'
    }

    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case 'net.minecraftforge.gradle.forge':
                    useModule("com.github.asbyth:ForgeGradle:${requested.version}")
                    break
                case 'org.spongepowered.mixin':
                    useModule("com.github.xcfrg:MixinGradle:${requested.version}")
                    break
            }
        }
    }
}

rootProject.name = 'EvergreenHUD'
include 'core'
include 'installer'
include 'base-modules'
include 'base-elements'

